# Adapted from the offical package https://github.com/NixOS/nixpkgs/blob/nixos-unstable/pkgs/tools/misc/opentelemetry-collector/builder.nix
{
  lib,
  buildGoModule,
  fetchFromGitHub,
}:
buildGoModule rec {
  pname = "ocb";
  # Also update `pkgs/tools/misc/opentelemetry-collector/releases.nix`
  # whenever that version changes.
  version = "0.145.0";

  src = fetchFromGitHub {
    owner = "open-telemetry";
    repo = "opentelemetry-collector";
    rev = "cmd/builder/v${version}";
    hash = "sha256-0o8FAtxM9SCGVsg/Ez5tU8XDptLJpqUteAn9CKWV0a0=";
  };

  sourceRoot = "${src.name}/cmd/builder";
  vendorHash = "sha256-wjM5YMJjsFc1crjE/EC+pieNIGkzZ28APVreVIOBf4g=";

  env.CGO_ENABLED = 0;
  ldflags = [
    "-s"
    "-w"
    "-X go.opentelemetry.io/collector/cmd/builder/internal.version=${version}"
  ];

  # Some tests download new dependencies for a modified go.mod. Nix doesn't allow network access so skipping.
  checkFlags = [
    "-skip TestGenerateAndCompile|TestReplaceStatementsAreComplete|TestVersioning"
  ];

  # Rename to ocb (it's generated as "builder")
  postInstall = ''
    mv $out/bin/builder $out/bin/ocb
  '';
}
